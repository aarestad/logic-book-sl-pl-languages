sentence_letter = { ASCII_ALPHA_UPPER }
tilde           = _{ "\u{223C}" } // ∼ (note: not the same as ~ :)
wedge           = _{ "\u{2227}" } // ∧
vel             = _{ "\u{2228}" } // ∨
horseshoe       = _{ "\u{2283}" } // ⊃
triple_bar      = _{ "\u{2261}" } // ≡
open_paren      = _{ "(" }
closed_paren    = _{ ")" }

negation               = { tilde      ~ sentence }
conjunction            = { open_paren ~ sentence ~ wedge  ~ sentence ~ closed_paren }
disjunction            = { open_paren ~ sentence ~ vel        ~ sentence ~ closed_paren }
material_conditional   = { open_paren ~ sentence ~ horseshoe  ~ sentence ~ closed_paren }
material_biconditional = { open_paren ~ sentence ~ triple_bar ~ sentence ~ closed_paren }

sentence = { sentence_letter | negation | conjunction | disjunction | material_conditional | material_biconditional }

top = { SOI ~ sentence ~ EOI }

WHITESPACE = _{ " " }
